import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'net.darkhax.curseforgegradle' version '1.1.18'
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"

    // Mod Dependencies
    modImplementation "com.cerbon:cerbons_api-fabric-${minecraft_version}:${cerbons_api_version}"

    //include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes}")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Useful Mods
    modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"

    modImplementation "curse.maven:jade-324717:${jade_version_fabric}"

    modCompileOnly("com.terraformersmc:modmenu:${modmenu_version_fabric}") {
        exclude module: "fabric-api"
    }
    modRuntimeOnly("com.terraformersmc:modmenu:${modmenu_version_fabric}") {
        exclude module: "fabric-api"
    }

    // All Projects Dependencies
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cardinal_components_version}" // Even though Cardinal Components is shipped with Cerbons API it needs to exist here too or Fabric will complain.
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${cardinal_components_version}"

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")))

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    var replaceProperties = [
            minecraft_version_range           : minecraft_version_range_fabric,
            fabric_loader_version_range       : fabric_loader_version_range,
            mod_id                            : mod_id,
            mod_name                          : mod_name,
            mod_license                       : mod_license,
            mod_version                       : mod_version,
            mod_authors                       : mod_authors,
            mod_description                   : mod_description,
            source_code_url                   : source_code_url,
            issues_url                        : issues_url,
            curseforge_url                    : curseforge_url,
            logo_file                         : logo_file,
            cerbons_api_version_range_fabric  : cerbons_api_version_range_fabric,
            cloth_config_version_range_fabric : cloth_config_version_range_fabric,
    ]
    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties + [project: project]
    }
}

base {
    archivesBaseName = archives_base_name + "-Fabric-${minecraft_version}"
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

tasks.register("publishCurseForgeAndModrinth", Task) {
    dependsOn publishCurseForge
    dependsOn tasks.modrinth
}

tasks.register("publishCurseForge", TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_project_id, remapJar)
    mainFile.addModLoader("Fabric")
    mainFile.addGameVersion(game_versions.replaceAll(" ", "").split(","))

    mainFile.releaseType = release_type
    mainFile.changelogType = changelog_type
    mainFile.changelog = rootProject.file(changelog).getText("UTF-8")

    mainFile.addRequirement(required_dependencies_fabric.replaceAll(" ", "").split(","))
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")

    projectId = modrinth_project_id
    uploadFile = remapJar
    loaders = ["fabric"]

    gameVersions = game_versions.replaceAll(" ", "").split(",") as Iterable<? extends String>

    versionName = archivesBaseName + "-" + mod_version
    versionNumber = mod_version
    versionType = release_type

    changelog = rootProject.file(project.changelog).getText('UTF-8')

    dependencies {
        required.project required_dependencies_fabric.replaceAll(" ", "").split(",")
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {

    }
}